import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'java'
    id 'org.openapi.generator' version '7.4.0'
    id 'maven-publish'
}

group = "in.lekhai"
version = "1.0.3"

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:3.3.0'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.17.1'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation 'io.swagger.core.v3:swagger-annotations-jakarta:2.2.21'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
}


def openApiRoot = file("$rootDir/openapi")
def generatedDir = "$rootDir/generated"
def basePackage = "in.lekhai"

openApiRoot.eachFileRecurse { specFile ->
    if (specFile.isFile() && (specFile.name.endsWith(".yaml") || specFile.name.endsWith(".yml"))) {

        // Derive package from folder path
        def relativePath = specFile.parentFile
                .path
                .replace(openApiRoot.path, "")
                .replace(File.separator, ".")
                .replaceAll("^\\.", "")

        def javaTaskName = "openApiGen_" + relativePath.replace(".", "_")
//        def javaOutputDir = "$generatedDir/generated-java/${relativePath.replace(".", "/")}"

        tasks.register(javaTaskName, GenerateTask) {


            generatorName = "spring"
            inputSpec = specFile.absolutePath
            outputDir = generatedDir

            apiPackage = "${basePackage}.${relativePath}.api"
            modelPackage = "${basePackage}.${relativePath}.dto"

            globalProperties = [
                    apis           : "",
                    models         : "",
                    supportingFiles: "false"
            ]

            configOptions = [
                    interfaceOnly       : "true",
                    useSpringBoot3      : "true",
                    useTags             : "true",
                    skipDefaultInterface: "true",
                    useRecords : "true"
            ]
        }

        //Dart
        def dartTaskName = "openApiGenDart_" + relativePath.replace(".", "_")
        def dartOutputDir = "$generatedDir/generated-dart/${relativePath.replace(".", "/")}"

        tasks.register(dartTaskName, GenerateTask) {
            generatorName = "dart-dio"
            inputSpec = specFile.absolutePath
            outputDir = dartOutputDir

            apiPackage = "${basePackage}.${relativePath}.api"
            modelPackage = "${basePackage}.${relativePath}.dto"

            globalProperties = [
                    apis           : "",
                    models         : "",
                    supportingFiles: "false",
                    apiTests       : "false",
                    modelTests     : "false",
                    apiDocs        : "false",
                    modelDocs      : "false"
            ]

            configOptions = [
                    allowUnicodeIddentifiers: "true",
                    useEnumExtension        : "true"
            ]
        }
    }
}


tasks.register('openApiGenerateAll') {
    dependsOn tasks.matching { it.name.startsWith("openApiGen_") }
}

sourceSets {
    main {
        java {
            srcDir "$generatedDir"
        }
    }
}

tasks.register('cleanGenerated', Delete) {
    delete generatedDir
}

compileJava.dependsOn openApiGenerateAll


jar {
    archiveBaseName.set("in.lekhai")
    archiveVersion.set("1.0.0")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = "lekhai-api-spec"
        }
    }
}
